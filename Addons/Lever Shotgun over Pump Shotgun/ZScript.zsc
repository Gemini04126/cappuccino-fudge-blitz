class CFBAddon_LeverShotgun : CFB_PumpShotgun replaces CFB_PumpShotgun
{
	Default
	{
		Tag "$TAG_CFBADDLSHOTGUN";
		Obituary "$KILLCFBADDLSHOTGUN";
		Inventory.PickupMessage "$GOTCFBADDLSHOTGUN";

		Weapon.UpSound "weapons/addons/lshotgun/draw";
		Inventory.PickupSound "weapons/addons/lshotgun/pickup";
		AttackSound "weapons/addons/lshotgun/fire";
	}
	States
	{
		Spawn:
			LSHG Z -1;
			Stop;
		Select:
			TNT1 A 0 A_QuickRaise();
			LSHG X 1 A_WeaponOffset(90, 122, WOF_INTERPOLATE);
			LSHG X 1 A_WeaponOffset(60, 92, WOF_INTERPOLATE);
			LSHG X 1 A_WeaponOffset(40, 72, WOF_INTERPOLATE);
			LSHG W 1 A_WeaponOffset(20, 52, WOF_INTERPOLATE);
			LSHG V 1 A_WeaponOffset(10, 42, WOF_INTERPOLATE);
			LSHG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
		Deselect:
			LSHG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			LSHG V 1 A_WeaponOffset(10, 42, WOF_INTERPOLATE);
			LSHG W 1 A_WeaponOffset(20, 52, WOF_INTERPOLATE);
			LSHG X 1 A_WeaponOffset(40, 72, WOF_INTERPOLATE);
			LSHG X 1 A_WeaponOffset(60, 92, WOF_INTERPOLATE);
			LSHG X 1 A_WeaponOffset(90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;
		Ready:
			LSHG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Fire:
			LSHG A 0 A_JumpIfNoAmmo("FireEmpty");
			LSHG B 1 Bright Offset(0, 36) {
				A_OverlayPivot(OverlayID(), 0.665, 0.5);
				A_FireBullets(2.8, 2.8, 7, 13, "CFB_ShotgunPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -2.5);
				A_Overlay(-2, "Flash");
				A_OverlayScale(OverlayID(), 1.1);
			}
			LSHG B 1 Bright Offset(0, 52) A_OverlayScale(OverlayID(), 1.3);
			LSHG B 2 Offset (0, 57) A_OverlayScale(OverlayID(), 1.4);
			LSHG B 2 Offset (0, 47) A_OverlayScale(OverlayID(), 1.2);
			LSHG B 2 Offset (0, 37) A_OverlayScale(OverlayID(), 1.1);
			LSHG B 2 Offset (0, 32) A_OverlayScale(OverlayID(), 1.0);
			LSHG CDE 2;
			LSHG FGHII 1;
			LSHG J 3 A_StartSound("weapons/addons/lshotgun/cock_back", CHAN_BODY);
			LSHG IIHGF 1;
			LSHG E 3 A_StartSound("weapons/addons/lshotgun/cock_forward", CHAN_BODY);
			LSHG DCA 2;
			LSHG A 2 A_ReFire;
			Goto Ready;
		FireEmpty:
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, "ReloadEmpty");
			LSHG B 3 Offset (0, 37) {
				A_StartSound("weapons/empty");
				A_OverlayPivot(OverlayID(), 0.665, 0.5);
				A_OverlayScale(OverlayID(), 1.1);
			}
			LSHG A 9 Offset (0, 32) A_OverlayScale(OverlayID(), 1.0);
			Goto Ready;
		Reload:
		// Check for shells, and if you're already full.
			TNT1 A 0 A_JumpIfInventory("CFB_PumpShotgunAmmo", 0, "Ready");
			TNT1 A 0 A_JumpIfNoAmmo("ReloadEmpty");
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, 1);
			Goto Ready;
			PSHG BCD 2;
			PSHG EF 1;
			PSHG F 3;
		Reload.Repeat:
			// if full on ammo, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_PumpShotgunAmmo", 0, "Reload.Finish");
			// if at least 1 shell to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, 1);
			Goto Reload.Finish;
			PSHG OPQ 2;
			// exchange shell for ammo
			TNT1 A 0 {
				A_GiveInventory("CFB_PumpShotgunAmmo", 1);
				A_Takeinventory("CFB_Shells",1);
			}
			PSHG R 2 A_StartSound("weapons/pshotgun/load");
			PSHG SSTT 1 A_JumpIf(IsPressed(BT_ATTACK), "Reload.Finish");
			Goto Reload.Repeat;
		Reload.Finish:
			PSHG U 2;
			PSHG FE 1;
			PSHG DCBA 2;
			Goto Ready;
		ReloadEmpty:
			// if at least 1 shell to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, 1);
			Goto Ready;
			PSHG BCD 2;
			PSHG EF 1;
			PSHG F 3;
			PSHG OPQ 2;
			// exchange shell for ammo
			TNT1 A 0 {
				A_GiveInventory("CFB_PumpShotgunAmmo", 1);
				A_Takeinventory("CFB_Shells",1);
			}
			PSHG R 2 A_StartSound("weapons/pshotgun/load");
			PSHG STU 2;
			PSHG FFFGHH 1;
			PSHG I 4 A_StartSound("weapons/pshotgun/pump_in", CHAN_BODY);
			PSHG HHG 1;
			PSHG F 3 A_StartSound("weapons/pshotgun/pump_out", CHAN_BODY);
			TNT1 A 0 A_JumpIf(IsPressed(BT_ATTACK), "Reload.Finish");
			Goto Reload.Repeat;
		Flash:
			LSHF A 1 Bright {
				A_AttachLightDef("Flash", "CFB_GunFlash");
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayOffset(OverlayID(), 0, -1);
				A_OverlayScale(OverlayID(), 0.7);
			}
			LSHF B 1 Bright {
				A_OverlayScale(OverlayID(), 0.9);
				A_OverlayOffset(OverlayID(), 0, -6);
			}
			LSHF C 1 Bright A_OverlayScale(OverlayID(), 1);
			TNT1 A 0 A_RemoveLight("Flash");
			Stop;
	}
}