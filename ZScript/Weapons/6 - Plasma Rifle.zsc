// Plasma Rifle
// It's a plasma rifle. :/
class CFB_PlasmaRifle : CFB_Weapon replaces PlasmaRifle
{
	int cooldown;
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 100;
		Weapon.AmmoType "CFB_Cell";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 40;
		
		Tag "$TAG_CFBPLASMARIFLE";
		// No Obituary defined here because this is a projectile weapon
		Inventory.PickupMessage "$GOTCFBPLASMARIFLE";

		//Weapon.UpSound "weapons/plasmarifle/draw";
		//Inventory.PickupSound "weapons/plasmarifle/pickup";
		// No AttackSound defined here because this is a projectile weapon
	}
	States
	{
		Spawn:
			PLSG Z -1;
			Stop;
		Select:
			TNT1 A 0 A_QuickRaise();
			PLSG L 1 A_WeaponOffset(90, 122, WOF_INTERPOLATE);
			PLSG L 1 A_WeaponOffset(60, 92, WOF_INTERPOLATE);
			PLSG L 1 A_WeaponOffset(40, 72, WOF_INTERPOLATE);
			PLSG K 1 A_WeaponOffset(20, 52, WOF_INTERPOLATE);
			PLSG J 1 A_WeaponOffset(10, 42, WOF_INTERPOLATE);
			PLSG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
		Deselect:
			PLSG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			PLSG J 1 A_WeaponOffset(10, 42, WOF_INTERPOLATE);
			PLSG K 1 A_WeaponOffset(20, 52, WOF_INTERPOLATE);
			PLSG L 1 A_WeaponOffset(40, 72, WOF_INTERPOLATE);
			PLSG L 1 A_WeaponOffset(60, 92, WOF_INTERPOLATE);
			PLSG L 1 A_WeaponOffset(90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;
		Ready:
			PLSG A 1 {
				A_WeaponReady();
				invoker.cooldown = 0;
			}
			Loop;
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("Fire.Empty");
			TNT1 A 0 {
				invoker.cooldown++;
				A_FireProjectile("CFB_PlasmaBall");
				A_StartSound("weapons/plasma/fire", CHAN_WEAPON);
			}
			TNT1 A 0 A_Jump(128, "Fire2");
			PLSG BC 1 Bright;
			Goto PostFire;
		Fire2:
			PLSG DE 1 Bright;
			Goto PostFire;
		PostFire:
			PLSG A 1 A_OverlayScale(OverlayID(), 1.0);
			PLSG A 0 A_ReFire;
			PLSG A 0 A_JumpIf(invoker.cooldown >= 6, "Cooldown"); // 5 shots
			Goto Ready;
		Cooldown:
			PLSG H 2;
			PLSG I 14;
			PLSG H 4;
			Goto Ready;
		Fire.Empty:
			PLSG F 1 A_StartSound("weapons/empty", CHAN_WEAPON);
			PLSG GA 1;
			Goto Ready;
	}
}
class CFB_PlasmaBall : Actor
{
	Default
	{
		+RANDOMIZE
		Decal "CFB_PlasmaScorch";
		Radius 13;
		Height 8;
		Speed 25;
		DamageFunction (25); // Slightly higher than average, because it's a better, more even number.
		Projectile;
		RenderStyle "Add";
		Alpha 0.75;
		DeathSound "weapons/plasma/explode";
		Obituary "$KILLCFBPLASMARIFLE";
	}
	States
	{
		Spawn:
			PLSP A 3 Bright {
				A_SpawnParticle("612cef", SPF_FULLBRIGHT|SPF_RELATIVE, 12, (random(5, 6)*3), random(-8, 8), random(-6, 6), random(-6, 6), velx: random(0, 4), vely: random(-2, 2), velz: random(-2, 2), accelx: random(-1, 0), startalphaf: 0.75, sizestep: -0.5);
				A_StartSound("weapons/plasma/projectile", CHAN_VOICE, CHANF_LOOPING);
			}
			PLSP B 3 Bright A_SpawnParticle("2000ba", SPF_FULLBRIGHT|SPF_RELATIVE, 12, (random(1, 2)*3), random(-8, 8), random(-6, 6), random(-6, 6), velx: random(0, 4), vely: random(-2, 2), velz: random(-2, 2), accelx: random(-1, 0), startalphaf: 0.75, sizestep: -0.5);
			PLSP C 3 Bright A_SpawnParticle("3d11d7", SPF_FULLBRIGHT|SPF_RELATIVE, 12, (random(3, 4)*3), random(-8, 8), random(-6, 6), random(-6, 6), velx: random(0, 4), vely: random(-2, 2), velz: random(-2, 2), accelx: random(-1, 0), startalphaf: 0.75, sizestep: -0.5);
			PLSP D 3 Bright A_SpawnParticle("793eff", SPF_FULLBRIGHT|SPF_RELATIVE, 12, (random(7, 8)*3), random(-8, 8), random(-6, 6), random(-6, 6), velx: random(0, 4), vely: random(-2, 2), velz: random(-2, 2), accelx: random(-1, 0), startalphaf: 0.75, sizestep: -0.5);
			Loop;
		Death:
			PLSP EFGHI 4 Bright;
			Stop;
	}
}