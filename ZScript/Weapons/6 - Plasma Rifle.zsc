// Plasma Rifle
// It's a plasma rifle. :/
class CFB_PlasmaRifle : CFB_Weapon replaces PlasmaRifle
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 100;
		Weapon.AmmoType "CFB_Cell";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 40;
		
		Tag "$TAG_CFBPLASMARIFLE";
		// No Obituary defined here because this is a projectile weapon
		Inventory.PickupMessage "$GOTCFBPLASMARIFLE";

		//Weapon.UpSound "weapons/plasmarifle/draw";
		//Inventory.PickupSound "weapons/plasmarifle/pickup";
		// No AttackSound defined here because this is a projectile weapon
	}
	States
	{
		Spawn:
			PLSG Z -1;
			Stop;
		Select:
			PLSG A 1 A_Raise(18); // Redo this later to be fancier? I think it'd be cool.
			Loop;
		Deselect:
			PLSG A 1 A_Lower(18); // Ditto.
			Loop;
		Ready:
			PLSG A 1 A_WeaponReady;
			Loop;
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("Fire.Empty");
			TNT1 A 0 A_FireProjectile("CFB_PlasmaBall"); // Replace later
			TNT1 A 0 A_Jump(128, "Fire2");
			PLSG BC 1 Bright;
			Goto PostFire;
		Fire2:
			PLSG DE 1 Bright;
			Goto PostFire;
		PostFire:
			PLSG A 1 A_OverlayScale(OverlayID(), 1.0);
			PLSG H 2 A_ReFire;
			PLSG I 16;
			PLSG H 2;
			Goto Ready;
		Fire.Empty:
			PLSG F 1 A_StartSound("weapons/empty", CHAN_WEAPON);
			PLSG GA 1;
			Goto Ready;
	}
}
class CFB_PlasmaBall : Actor
{
	Default
	{
		+RANDOMIZE
		Decal "PlasmaScorch";
		Radius 13;
		Height 8;
		Speed 25;
		DamageFunction (25); // Slightly higher than average, because it's a better, more even number.
		Projectile;
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
		Obituary "$KILLCFBPLASMARIFLE";
	}
	States
	{// Replace later.
		Spawn:
			PLSS AB 6 Bright;
			Loop;
		Death:
			PLSE ABCDE 4 Bright;
			Stop;
	}
}