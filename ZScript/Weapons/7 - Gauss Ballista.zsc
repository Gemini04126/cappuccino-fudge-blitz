// Gauss Ballista
// "Just make the BFG a gauss cannon then lol" - Matt Eldrydge
// Primary: Explosive railgun projectile.
// Secondary: Calamity Blade.
class CFB_GaussBallista : CFB_Weapon replaces BFG9000
{
	Default
	{
		+WEAPON.BFG
		//+WEAPON.NOAUTOFIRE
		Weapon.SlotNumber 7;
		Weapon.SelectionOrder 2800;
		Weapon.AmmoType "CFB_Cell";
		Weapon.AmmoUse 10;
		Weapon.AmmoGive 40;

		Tag "$TAG_GAUSSBALLISTA";
		// No Obituary defined here because this is a projectile weapon (and has two attacks)
		Inventory.PickupMessage "$GOTGAUSSBALLISTA";

		//Weapon.UpSound "";
		//Inventory.PickupSound "";
		// No AttackSound defined here because this is a projectile weapon (and has two attacks)
	}
	States
	{
		Spawn:
			GBAG Z -1;
			Stop;
		Select:
			TNT1 A 0 A_Overlay(2, "Spinner", true);
			GBAG A 1 A_Raise(18);
			Loop;
		Deselect:
			GBAG A 1 A_Lower(18);
			Loop;
		Ready:
			TNT1 A 0 A_Overlay(2, "Spinner", true);
			TNT1 A 0 A_JumpIfNoAmmo("Ready.Empty");
			GBAG A 1 A_WeaponReady;
			Loop;
		Ready.Empty:
			TNT1 A 0 A_JumpIfInventory("CFB_Cell", 10, "Ready.Charging");
			TNT1 A 0 A_Overlay(2, "StopSpinner");
			GBAG B 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY); // no firing. nooooo firing.
			Loop;
		Ready.Charging:
			TNT1 A 0 A_JumpIfNoAmmo("Ready.Empty");
			TNT1 A 0 A_Overlay(2, "Spinner", true);
			GBAG CDEFGHIJK 3/* A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOSWITCH)*/;
			Goto Ready;
		Fire:
			GBAG A 2 {
				A_FireRailgun();
				A_Overlay(-2, "PrimaryFlash");
			}
			GBAG B 6 A_Overlay(2, "StopSpinner");
			Goto Ready.Charging;
		Spinner:
			GBAL ABCDE 2;
			Loop;
		StopSpinner:
			Stop;
		PrimaryFlash:
			GBAF AB 1;
			Stop;
	}
}