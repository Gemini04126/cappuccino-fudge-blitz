// Shotgun
// Boomstick. Yeh! Has reloading by majority vote.
class CFB_ShotgunAmmo : Ammo
{
	Default
	{
		Inventory.Icon "IPUMA0";
		Inventory.Amount 0;
		Inventory.MaxAmount 9;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 9;
		+INVENTORY.IGNORESKILL
	}
}
class CFB_Shotgun : CFB_Weapon replaces Shotgun
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 3;
		Weapon.AmmoType1 "CFB_ShotgunAmmo";
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "Shell";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 8;

		Tag "$TAG_SHOTGUN";
		Obituary "$KILLSHOTGUN";
		Inventory.PickupMessage "$GOTSHOTGUN";

		Weapon.UpSound "weapons/shotgun/draw";
		Inventory.PickupSound "weapons/shotgun/pickup";
		AttackSound "weapons/shotgun/fire";

		//Decal "BulletChip";
		DamageType "Shotgun";
	}
	States
	{
		Spawn:
			SHTG Z -1;
			Stop;
		Select:
			SHTG A 1 A_Raise(18); // Redo this later to be fancier? I think it'd be cool.
			Loop;
		Deselect:
			SHTG A 1 A_Lower(18); // Ditto.
			Loop;
		Ready:
			SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD); // Possible reloading later. Who knows!
			Loop;
		Fire:
			SHTG A 0 A_JumpIfNoAmmo("FireEmpty");
			SHTG J 1 Bright {
				A_OverlayPivot(OverlayID(), 0.56, 0.5);
				A_FireBullets(2.8, 2.8, 7, 13, flags: FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -2.5);
				A_GunFlash();
				A_OverlayScale(OverlayID(), 1.1);
			}
			SHTG K 1 Bright A_OverlayScale(OverlayID(), 1.3);
			SHTG L 2 A_OverlayScale(OverlayID(), 1.4);
			SHTG M 2 A_OverlayScale(OverlayID(), 1.2);
			SHTG N 2 A_OverlayScale(OverlayID(), 1.1);
			SHTG A 2 A_OverlayScale(OverlayID(), 1.0);
			SHTG BCD 2;
			SHTG EFGHH 1;
			SHTG I 4 A_StartSound("weapons/shotgun/pump_in", CHAN_BODY);
			SHTG HHGFE 1;
			SHTG D 2 A_StartSound("weapons/shotgun/pump_out", CHAN_BODY);
			SHTG CBA 2;
			SHTG A 2 A_ReFire;
			Goto Ready;
		FireEmpty:
			TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadEmpty");
			SHTG N 3 A_StartSound("weapons/empty");
			SHTG A 9;
			Goto Ready;
		Reload:
		// Check for shells, and if you're already full.
			TNT1 A 0 A_JumpIfInventory("CFB_ShotgunAmmo", 0, "Ready");
			TNT1 A 0 A_JumpIfNoAmmo("ReloadEmpty");
			TNT1 A 0 A_JumpIfInventory("Shell", 1, 1);
			Goto Ready;
			SHTG BCD 2;
			SHTG EF 1;
			SHTG F 3;
		Reload.Repeat:
			// if full on ammo, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_ShotgunAmmo", 0, "Reload.Finish");
			// if at least 1 shell to load, continue
			TNT1 A 0 A_JumpIfInventory("Shell", 1, 1);
			Goto Reload.Finish;
			SHTG OPQ 2;
			// exchange shell for ammo
			TNT1 A 0 {
				A_GiveInventory("CFB_ShotgunAmmo", 1);
				A_Takeinventory("Shell",1);
			}
			SHTG R 2 A_StartSound("weapons/shotgun/load");
			SHTG SSTT 1 A_JumpIf(IsPressed(BT_ATTACK), "Reload.Finish");
			Goto Reload.Repeat;
		Reload.Finish:
			SHTG U 2;
			SHTG FE 1;
			SHTG DCBA 2;
			Goto Ready;
		ReloadEmpty:
			SHTG BCD 2;
			SHTG EF 1;
			SHTG F 3;
		ReloadEmpty.Repeat:
			// if full on ammo, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_ShotgunAmmo", invoker.ammo1.MaxAmount - 1, "ReloadEmpty.Finish");
			// if at least 1 shell to load, continue
			TNT1 A 0 A_JumpIfInventory("Shell", 1, 1);
			Goto ReloadEmpty.Finish;
			SHTG OPQ 2;
			// exchange shell for ammo
			TNT1 A 0 {
				A_GiveInventory("CFB_ShotgunAmmo", 1);
				A_Takeinventory("Shell",1);
			}
			SHTG R 2 A_StartSound("weapons/shotgun/load");
			SHTG SSTT 1 A_JumpIf(IsPressed(BT_ATTACK), "ReloadEmpty.Finish");
			Goto ReloadEmpty.Repeat;
		ReloadEmpty.Finish:
			SHTG U 2;
			SHTG FFFGHH 1;
			SHTG I 4 A_StartSound("weapons/shotgun/pump_in", CHAN_BODY);
			SHTG HHGFE 1;
			SHTG D 2 A_StartSound("weapons/shotgun/pump_out", CHAN_BODY);
			SHTG CBA 2;
			Goto Ready;
		Flash:
			TNT1 A 2 A_AttachLightDef("Flash", "CFB_GunFlash");
			TNT1 B 0 A_RemoveLight("Flash");
			Stop;
	}
}