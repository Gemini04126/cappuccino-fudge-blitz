// Brass Knuckles
// ...but only on one hand. You only had one pair! (read: no idle sprite with knuckles on :[)
class CFB_BrassKnuckles : CFB_Weapon replaces Fist
{
	Default
	{
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
		+WEAPON.NOAUTOSWITCHTO
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 3700;

		Tag "$TAG_BRASSKNUCKLES";
		Obituary "$KILLBRASSKNUCKLES";

		Inventory.Icon "PUNCZ0"; // for stuff like Gearbox
	}
	States
	{
		Select:
			TNT1 A 0 A_QuickRaise();
			PUNC A 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
		Deselect:
			PUNC A 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;
		Ready:
			PUNC A 1 A_WeaponReady;
			Loop;
		Fire:
			PUNC A 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			Goto Fire.Right;
		Fire.Right:
			TNT1 A 1 A_StartSound("weapons/bknuckles/swing", CHAN_WEAPON);
			PUNC BD 1;
			PUNC F 1 {
				int dmg = 13;
				if (FindInventory("CFB_PowerStrength"))
					A_CustomPunch(dmg*10, true, pufftype: "CFB_BerserkPuff", range: meleeRange + 20, meleesound: "weapons/bknuckles/hit");
				else
					A_CustomPunch(dmg, true, pufftype: "CFB_BulletPuff", range: meleeRange + 20, meleesound: "weapons/bknuckles/hit");
			}
			PUNC GGFFF 1;
			PUNC EDCB 1;
			TNT1 A 0 A_ReFire("Fire.Left");
			Goto Raise;
		Fire.Left:
			TNT1 A 1 A_StartSound("weapons/bknuckles/swing", CHAN_WEAPON);
			PUNC HJ 1;
			PUNC L 1 {
				int dmg = 11;
				if (FindInventory("CFB_PowerStrength"))
					A_CustomPunch(dmg*10, true, pufftype: "CFB_BerserkPuff", range: meleeRange + 20, meleesound: "weapons/bknuckles/hit");
				else
					A_CustomPunch(dmg, true, pufftype: "CFB_BulletPuff", range: meleeRange + 20, meleesound: "weapons/bknuckles/hit");
			}
			PUNC MMLLL 1;
			PUNC KJI 1;
			TNT1 A 0 A_ReFire("Fire.LeftToRight");
			Goto Ready;
		Fire.LeftToRight:
			PUNC H 2;
			Goto Fire.Right;
		Raise:
			PUNC A 1 A_WeaponOffset(-90, 122);
			PUNC A 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PUNC A 0 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
	}
}
class CFB_BerserkPuff : CFB_BulletPuff
{
	Default
	{
		+PUFFONACTORS
		DamageType "Rocket"; // for Supercharge compatibility
	}
	States
	{
		XDeath:
			TNT1 A 0 A_StartSound("weapons/bknuckles/break");
			Stop;
	}
}