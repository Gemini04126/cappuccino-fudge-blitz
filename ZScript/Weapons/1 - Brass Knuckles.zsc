class CFB_BrassKnuckles : CFB_Weapon replaces Fist
{
	Default
	{
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
		+WEAPON.NOAUTOSWITCHTO
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 3700;

		Tag "$TAG_BRASSKNUCKLES";
		Obituary "$KILLBRASSKNUCKLES";
	}
	States
	{
		Select:
			TNT1 A 0 A_QuickRaise();
			PUNC A 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
		Deselect:
			PUNC A 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;
		Ready:
			PUNC A 1 A_WeaponReady;
			Loop;
		Fire:
			PUNC A 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			Goto Fire.Right;
		Fire.Right:
			TNT1 A 1;
			PUNC BD 1;
			PUNC F 1 {
				int dmg = 13;
				if (FindInventory("CFB_PowerStrength"))
				{
					dmg *= 10;
				}
				A_CustomPunch(dmg, norandom:true, range: meleeRange + 20, meleesound: "*fist");
			}
			PUNC GGFFF 1;
			PUNC EDCB 1;
			TNT1 A 0 A_ReFire("Fire.Left");
			Goto Raise;
		Fire.Left:
			TNT1 A 1;
			PUNC HJ 1;
			PUNC L 1 {
				int dmg = 11;
				if (FindInventory("CFB_PowerStrength"))
				{
					dmg *= 10;
				}
				A_CustomPunch(dmg, norandom:true, range: meleeRange + 20, meleesound: "*fist");
			}
			PUNC MMLLL 1;
			PUNC KJI 1;
			TNT1 A 0 A_ReFire("Fire.LeftToRight");
			Goto Ready;
		Fire.LeftToRight:
			PUNC H 2;
			Goto Fire.Right;
		Raise:
			PUNC A 1 A_WeaponOffset(-90, 122);
			PUNC A 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PUNC A 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PUNC A 0 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
	}
}