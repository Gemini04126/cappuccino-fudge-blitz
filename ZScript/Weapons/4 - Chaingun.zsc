// Chaingun
// It shoot.
class CFB_Chaingun : CFB_Weapon //replaces Chaingun // Removed because it's randomly spawned through the event handler
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		+WEAPON.ALT_AMMO_OPTIONAL
		+WEAPON.ALT_USES_BOTH
		Weapon.SlotNumber 4;
		Weapon.SelectionOrder 700;
		Weapon.AmmoType "CFB_Magazine";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;

		Tag "$TAG_CFBCHAINGUN";
		Obituary "$KILLCFBCHAINGUN";
		Inventory.PickupMessage "$GOTCFBCHAINGUN";

		Weapon.UpSound "weapons/chaingun/draw";
		Inventory.PickupSound "weapons/chaingun/pickup";
		AttackSound "weapons/chaingun/fire";
	}
	States
	{
		Spawn:
			CHGG Z -1;
			Stop;
		Select:
			CHGG A 1 A_Raise(18); // Redo this later to be fancier? I think it'd be cool.
			Loop;
		Deselect:
			CHGG A 1 A_Lower(18); // Ditto.
			Loop;
		Ready:
			CHGG A 1 A_WeaponReady;
			Loop;
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("Fire.Empty");
			TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 0, "Fire.Haste");
			CHGG A 1 {
				A_OverlayPivot(OverlayID(), 0.64, 0);
				A_FireBullets(2.8, 2.8, 1, 15, "CFB_ChaingunPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -1.5);
				A_Overlay(-2, "Flash");
				A_OverlayScale(OverlayID(), 1.05);
			}
			CHGG B 1 A_OverlayScale(OverlayID(), 1.2);
			CHGG C 1 A_OverlayScale(OverlayID(), 1.1);
			CHGG D 1 A_OverlayScale(OverlayID(), 1.0);
			CHGG A 0 A_ReFire;
			CHGG ABCDABBCCCDDDDAAAAABBBBBBCCCCCCCDDDDDDDD 1 A_WeaponReady;
			Goto Ready;
		Fire.Empty:
			CHGG A 1 A_StartSound("weapons/empty");
			CHGG BCD 1;
			CHGG A 0 A_ReFire;
			CHGG ABCDABBCCCDDDDAAAAABBBBBBCCCCCCCDDDDDDDD 1 A_WeaponReady;
			Goto Ready;
		Fire.Haste:
			CHGG A 1 {
				A_OverlayPivot(OverlayID(), 0.64, 0);
				A_FireBullets(2.8, 2.8, 1, 15, "CFB_ChaingunPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -1.5);
				A_Overlay(-2, "Flash");
				A_OverlayScale(OverlayID(), 1.2);
			}
			CHGG C 1 A_OverlayScale(OverlayID(), 1.05);
			CHGG A 0 A_OverlayScale(OverlayID(), 1.0);
			CHGG A 0 A_ReFire;
			CHGG ABCDABBCCCDDDDAAAAABBBBBBCCCCCCCDDDDDDDD 1 A_WeaponReady;
			Goto Ready;
		AltFire:
			TNT1 A 0 A_JumpIfNoAmmo("AltFire.Empty");
			CHGG A 2 {
				A_OverlayPivot(OverlayID(), 0.64, 0);
				A_FireBullets(0, 0, 1, 15, "CFB_ChaingunPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -1.5);
				A_Overlay(-2, "Flash");
				A_OverlayScale(OverlayID(), 1.05);
			}
			CHGG B 2 A_OverlayScale(OverlayID(), 1.2);
			CHGG C 2 A_OverlayScale(OverlayID(), 1.1);
			CHGG D 2 A_OverlayScale(OverlayID(), 1.0);
			CHGG A 0 A_ReFire;
			CHGG ABCDAABBBCCCCDDDDD 2 A_WeaponReady;
			Goto Ready;
		AltFire.Empty:
			CHGG A 2 A_StartSound("weapons/empty");
			CHGG BCD 2;
			CHGG A 0 A_ReFire;
			CHGG AABBCCDDAABBCCCDDDDAAAAABBBBBBCCCCCCCDDDDDDDD 1 A_WeaponReady;
			Goto Ready;
		Flash:
			CHGF A 1 Bright A_AttachLightDef("Flash", "CFB_GunFlash");
			CHGF B 1 Bright;
			TNT1 A 0 A_RemoveLight("Flash");
			Stop;
	}
}
class CFB_ChaingunPuff : CFB_BulletPuff
{
	Default
	{
		DamageType "Chaingun";
	}
}