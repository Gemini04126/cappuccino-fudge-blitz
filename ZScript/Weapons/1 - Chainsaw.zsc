// Chainsaw
// Not really replaced yet, visually, but it at least works for now.
class CFB_Chainsaw : CFB_Weapon replaces Chainsaw
{
	action void A_ChainsawOffset()
	{
		A_WeaponOffset(0+frandom(-2.5, 2.5), 32+frandom(0, 2.5));
	}
	Default
	{
		+WEAPON.MELEEWEAPON
		+WEAPON.NOAUTOSWITCHTO
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 2200;
		Weapon.Kickback 0;

		Tag "$TAG_CFBCHAINSAW";
		Obituary "$KILLCFBCHAINSAW";
		Inventory.PickupMessage "$GOTCFBCHAINSAW";

		Inventory.PickupSound "weapons/chainsaw/pickup";
	}
	States
	{
		Spawn:
			CHNS Z -1;
			Stop;
		Select:
			TNT1 A 0 A_OverlayScale(OverlayID(), 1, 1, WOF_INTERPOLATE);
			CHNS A 1 A_Raise(18);
			Loop;
		Deselect:
			TNT1 A 0 {
				A_OverlayScale(OverlayID(), 1, 1, WOF_INTERPOLATE);
				A_StopSound(CHAN_WEAPON);
			}
			CHNS A 1 A_Lower(18);
			Loop;
		Ready:
			CHNS A 0 {
				A_OverlayPivot(OverlayID(), 0.5, 0.45);
				A_WeaponOffset(0, 32);
			}
			CHNS A 1 {
				A_WeaponReady();
				A_OverlayScale(OverlayID(), 1.035, 1.035, WOF_INTERPOLATE);
				A_StartSound("weapons/chainsaw/idle", CHAN_WEAPON, CHANF_LOOPING);
			}
			CHNS AAABBBB 1 {
				A_WeaponReady();
				A_OverlayScale(OverlayID(), -0.005, -0.005, WOF_ADD);
			}
			Loop;
		Fire: // copy of Hold for the sake of a looping sound...
			TNT1 A 0 {
				A_OverlayScale(OverlayID(), 1, 1);
				A_StopSound(CHAN_WEAPON);
			}
			CHNS C 1 {
				A_Saw("", "weapons/chainsaw/hit", 10, "CFB_ChainsawPuff", SF_NORANDOM);
				A_ChainsawOffset();
				A_StartSound("weapons/chainsaw/start", CHAN_BODY);
			}
			CHNS CDD 1 A_ChainsawOffset();
			CHNS B 0 A_ReFire;
			Goto Release;
		Hold:
			CHNS C 1 {
				A_Saw("", "weapons/chainsaw/hit", 10, "CFB_ChainsawPuff", SF_NORANDOM);
				A_ChainsawOffset();
				A_StartSound("weapons/chainsaw/loop", CHAN_BODY, CHANF_LOOPING);
			}
			CHNS CDD 1 A_ChainsawOffset();
			CHNS B 0 A_ReFire;
			Goto Release;
		Release:
			TNT1 A 0 A_StartSound("weapons/chainsaw/stop", CHAN_BODY);
			Goto Ready;
	}
}
class CFB_ChainsawPuff : CFB_BulletPuff
{
	Default
	{
		DamageType "Saw"; // for Supercharge compatibility
	}
	States
	{
		XDeath:
			TNT1 A 0 A_StartSound("weapons/chainsaw/hit");
			Stop;
	}
}