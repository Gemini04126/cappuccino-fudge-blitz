// Pump Shotgun
// Boomstick. Yeh! Has reloading by majority vote.
class CFB_PumpShotgunAmmo : Ammo
{
	Default
	{
		Inventory.Icon "PSHGZ0"; // Replace later
		Inventory.Amount 0;
		Inventory.MaxAmount 9;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 9;
		+INVENTORY.IGNORESKILL
	}
}
class CFB_PumpShotgun : CFB_Weapon replaces Shotgun
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 3;
		Weapon.SelectionOrder 1300;
		Weapon.AmmoType1 "CFB_PumpShotgunAmmo";
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "CFB_Shells";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 8;

		Tag "$TAG_PSHOTGUN";
		Obituary "$KILLPSHOTGUN";
		Inventory.PickupMessage "$GOTPSHOTGUN";

		Weapon.UpSound "weapons/pshotgun/draw";
		Inventory.PickupSound "weapons/pshotgun/pickup";
		AttackSound "weapons/pshotgun/fire";
	}
	States
	{
		Spawn:
			PSHG Z -1;
			Stop;
		Select:
			TNT1 A 0 A_QuickRaise();
			PSHG X 1 A_WeaponOffset(90, 122, WOF_INTERPOLATE);
			PSHG X 1 A_WeaponOffset(60, 92, WOF_INTERPOLATE);
			PSHG X 1 A_WeaponOffset(40, 72, WOF_INTERPOLATE);
			PSHG W 1 A_WeaponOffset(20, 52, WOF_INTERPOLATE);
			PSHG V 1 A_WeaponOffset(10, 42, WOF_INTERPOLATE);
			PSHG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
		Deselect:
			PSHG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			PSHG V 1 A_WeaponOffset(10, 42, WOF_INTERPOLATE);
			PSHG W 1 A_WeaponOffset(20, 52, WOF_INTERPOLATE);
			PSHG X 1 A_WeaponOffset(40, 72, WOF_INTERPOLATE);
			PSHG X 1 A_WeaponOffset(60, 92, WOF_INTERPOLATE);
			PSHG X 1 A_WeaponOffset(90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;
		Ready:
			PSHG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Fire:
			PSHG A 0 A_JumpIfNoAmmo("FireEmpty");
			PSHG J 1 Bright {
				A_OverlayPivot(OverlayID(), 0.56, 0.5);
				A_FireBullets(2.8, 2.8, 7, 13, "CFB_ShotgunPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -2.5);
				A_GunFlash();
				A_OverlayScale(OverlayID(), 1.1);
			}
			PSHG K 1 Bright A_OverlayScale(OverlayID(), 1.3);
			PSHG L 2 A_OverlayScale(OverlayID(), 1.4);
			PSHG M 2 A_OverlayScale(OverlayID(), 1.2);
			PSHG N 2 A_OverlayScale(OverlayID(), 1.1);
			PSHG A 2 A_OverlayScale(OverlayID(), 1.0);
			PSHG BCD 2;
			PSHG EFGHH 1;
			PSHG I 4 A_StartSound("weapons/pshotgun/pump_in", CHAN_BODY);
			PSHG HHGFE 1;
			PSHG D 2 A_StartSound("weapons/pshotgun/pump_out", CHAN_BODY);
			PSHG CBA 2;
			PSHG A 2 A_ReFire;
			Goto Ready;
		FireEmpty:
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, "ReloadEmpty");
			PSHG N 3 {
				A_StartSound("weapons/empty");
				A_OverlayPivot(OverlayID(), 0.56, 0.5);
				A_OverlayScale(OverlayID(), 1.1);
			}
			PSHG A 9 A_OverlayScale(OverlayID(), 1.0);
			Goto Ready;
		Reload:
		// Check for shells, and if you're already full.
			TNT1 A 0 A_JumpIfInventory("CFB_PumpShotgunAmmo", 0, "Ready");
			TNT1 A 0 A_JumpIfNoAmmo("ReloadEmpty");
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, 1);
			Goto Ready;
			PSHG BCD 2;
			PSHG EF 1;
			PSHG F 3;
		Reload.Repeat:
			// if full on ammo, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_PumpShotgunAmmo", 0, "Reload.Finish");
			// if at least 1 shell to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, 1);
			Goto Reload.Finish;
			PSHG OPQ 2;
			// exchange shell for ammo
			TNT1 A 0 {
				A_GiveInventory("CFB_PumpShotgunAmmo", 1);
				A_Takeinventory("CFB_Shells",1);
			}
			PSHG R 2 A_StartSound("weapons/pshotgun/load");
			PSHG SSTT 1 A_JumpIf(IsPressed(BT_ATTACK), "Reload.Finish");
			Goto Reload.Repeat;
		Reload.Finish:
			PSHG U 2;
			PSHG FE 1;
			PSHG DCBA 2;
			Goto Ready;
		ReloadEmpty:
			// if at least 1 shell to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Shells", 1, 1);
			Goto Ready;
			PSHG BCD 2;
			PSHG EF 1;
			PSHG F 3;
			PSHG OPQ 2;
			// exchange shell for ammo
			TNT1 A 0 {
				A_GiveInventory("CFB_PumpShotgunAmmo", 1);
				A_Takeinventory("CFB_Shells",1);
			}
			PSHG R 2 A_StartSound("weapons/pshotgun/load");
			PSHG STU 2;
			PSHG FFFGHH 1;
			PSHG I 4 A_StartSound("weapons/pshotgun/pump_in", CHAN_BODY);
			PSHG HHG 1;
			PSHG F 3 A_StartSound("weapons/pshotgun/pump_out", CHAN_BODY);
			TNT1 A 0 A_JumpIf(IsPressed(BT_ATTACK), "Reload.Finish");
			Goto Reload.Repeat;
		Flash:
			TNT1 A 2 A_AttachLightDef("Flash", "CFB_GunFlash");
			TNT1 A 0 A_RemoveLight("Flash");
			Stop;
	}
}
class CFB_ShotgunPuff : CFB_BulletPuff
{
	Default
	{
		DamageType "Shotgun";
	}
}