// Flasher
// I wanted a quick and easy Incinerator replacement...
// ... That's it. I saw this one, I fuck with it, and it's fine.
class CFB_Flasher : CFB_Weapon // Randomly spawned through the event handler
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		+WEAPON.NOAUTOFIRE
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 1800;
		Weapon.AmmoType "CFB_Cell";
		Weapon.AmmoUse 10;
		Weapon.AmmoGive 40;

		Tag "$TAG_CFBFLASHER";
		// No Obituary defined here because this is a projectile weapon
		Inventory.PickupMessage "$GOTCFBFLASHER";

		Weapon.UpSound "weapons/flasher/draw";
		Inventory.PickupSound "weapons/flasher/pickup";
		// No AttackSound defined here because this is a projectile weapon
	}
	States
	{
		Spawn:
			UCFL Z -1;
			Stop;
		Select:
			UCFL A 1 A_Raise(18);
			Loop;
		Deselect:
			UCFL A 1 A_Lower(18);
			Loop;
		Ready:
			UCFL A 1 A_WeaponReady;
			Loop;
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("Fire.Empty");
			UCFL A 2 Offset(0,45) {
				A_Overlay(-2, "Flash");
				A_OverlayFlags(-2, PSPF_POWDOUBLE|PSPF_CVARFAST|PSPF_ALPHA|PSPF_RENDERSTYLE, true);
				A_OverlayRenderStyle(-2, STYLE_ADD);
				A_OverlayPivot(OverlayID(), 0.65, 0);
				A_OverlayScale(OverlayID(), 1.4);
				A_StartSound("weapons/flasher/fire", CHAN_WEAPON);
				A_FireProjectile("CFB_FlasherProjectile");
				A_FireProjectile("CFB_FlasherProjectile", -4, false);
				A_FireProjectile("CFB_FlasherProjectile", 4, false);
			}
			UCFL A 2 Offset(0,40) A_OverlayScale(OverlayID(), 1.35);
			UCFL A 2 Offset(0,37) A_OverlayScale(OverlayID(), 1.3);
			UCFL A 2 Offset(0,35) A_OverlayScale(OverlayID(), 1.25);
			UCFL A 2 Offset(0,34) A_OverlayScale(OverlayID(), 1.15);
			UCFL A 2 Offset(0,33) A_OverlayScale(OverlayID(), 1.1);
			UCFL A 12 Offset(0,32) A_OverlayScale(OverlayID(), 1.0);
			TNT1 A 0 A_ReFire;
			Goto Ready;
		Fire.Empty:
			UCFL A 1 A_StartSound("weapons/empty", CHAN_WEAPON);
			Goto Ready;
		Flash:
			UCFF A 2 Bright;
			UCFF B 2 Bright;
			UCFF CDEF 2 Bright;
			Stop;
	}
}
class CFB_FlasherProjectile : Actor
{
	Default
	{
		+RANDOMIZE
		+NOEXTREMEDEATH //I think it looks nicer that it doesn't gib zombiemen when you shoot them with it
		Radius 12;
		Height 8;
		Speed 35;
		DamageFunction (90); // De-randomized damage
		Projectile;
		Decal "Scorch";
		DamageType "Fire"; // Supercharge doesn't have a default "Fire" death type, but it does have stuff like PyroFire, VileFire, FireTrap...
		RenderStyle "Add";
		DeathSound "weapons/flasher/sizzle";
		Obituary "$KILLCFBFLASHER";
	}
	States
	{
		Spawn:
			DMFL AB 2 Bright A_SpawnItemEx("CFB_FlasherTrail"); //The flasher trails are just for aesthetic purposes
			TNT1 A 0 A_SpawnItemEx("RocketSmokeTrail", zofs: -4); // Replaces +ROCKETTRAIL for a nicer effect
			Loop;
		Death:
			DMFL D 4 Bright A_Explode(25,128);
			DMFL EEFF 2 Bright A_SpawnProjectile("CFB_FlasherTrail2",0,0,random(-180,180),0,random(-180,180)); //randomly throws around flashertrails in death
			DMFL GGHH 2 Bright A_SpawnProjectile("CFB_FlasherTrail2",0,0,random(-180,180),0,random(-180,180)); //it's spaced out to spawn multiples of them
			Stop;
	}
}

class CFB_FlasherTrail : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+NOTELEPORT
		+CANNOTPUSH
		+NODAMAGETHRUST
		Speed 0;
		RenderStyle "Add";
		Alpha 0.6;
	}
	States
	{
		Spawn:
			DMFL AB 2 Bright A_Setscale(0.8,0.8); //Slowly getting smaller...
			DMFL AB 2 Bright A_Setscale(0.6,0.6);
			DMFL AB 2 Bright A_Setscale(0.4,0.4);
			DMFL AB 2 Bright A_Setscale(0.2,0.2);
		Death:
			Stop;
	}
}
class CFB_FlasherTrail2 : CFB_FlasherTrail
{
	Default
	{
		Speed 10; // some speed so when they die they shoot out, but essentially the same actor
	}
}