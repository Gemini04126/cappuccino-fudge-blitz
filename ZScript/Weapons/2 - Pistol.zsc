// Pistol
// It do be a pistol tho. Has reloading.
class CFB_PistolAmmo : Ammo
{
	Default
	{
		Inventory.Icon "PISGZ0"; // Replace later
		Inventory.Amount 0;
		Inventory.MaxAmount 10;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 10;
		+INVENTORY.IGNORESKILL
	}
}
class CFB_Pistol : CFB_Weapon replaces Pistol
{
	action state A_ReFirePistol()
	{
		if (JustPressed(BT_ATTACK))
			return ResolveState("Fire");
		else if (JustPressed(BT_ALTATTACK))
			return ResolveState("AltFire");
		else
			return ResolveState(null);
	}
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 2;
		Weapon.AmmoType1 "CFB_PistolAmmo";
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "CFB_Magazine";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 10;

		Tag "$TAG_PISTOL";
		Obituary "$KILLPISTOL";
		Inventory.PickupMessage "$GOTPISTOL";

		Weapon.UpSound "weapons/pistol/draw";
		Inventory.PickupSound "weapons/pistol/pickup";
		AttackSound "weapons/pistol/fire";
	}
	States
	{
		Spawn:
			PISG Z -1;
			Stop;

		Select:
			TNT1 A 0 A_QuickRaise();
			TNT1 A 0 A_JumpIfNoAmmo("SelectEmpty");
			PISG G 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
		SelectEmpty:
			PISG H 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG F 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto ReadyEmpty;

		Deselect:
			TNT1 A 0 A_JumpIfNoAmmo("DeselectEmpty");
			PISG G 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG G 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;
		DeselectEmpty:
			PISG F 1;
			PISG H 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG H 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;

		Ready:
			TNT1 A 0 A_JumpIfNoAmmo("ReadyEmpty");
			PISG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		ReadyEmpty:
			PISG F 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;

		Fire:
			PISG A 0 A_JumpIfNoAmmo("FireEmpty");
			PISG B 1 Bright {
				A_FireBullets(0.8, 0.8, -1, 16, "CFB_BulletPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -2.5);
				A_GunFlash();
			}
			PISG C 1 Bright;
			// Go to "Empty" reload state if nothing's in the mag
			PISG A 0 A_JumpIfNoAmmo("PostFireEmpty");
			Goto PostFire;
		PostFire:
			PISG D 3;
			PISG EEEEEEDDDAA 1 A_ReFirePistol;
			Goto Ready;
		FireEmpty:
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, "ReloadEmpty");
			PISG F 2 A_StartSound("weapons/pistol/empty");
			Goto PostFireEmpty;
		PostFireEmpty:
			PISG DEED 3;
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, "ReloadEmpty");
			Goto Ready;

		Reload:
		// Check for bullets, and if you're already full.
			TNT1 A 0 A_JumpIfInventory("CFB_PistolAmmo", 0, "Ready");
			TNT1 A 0 A_JumpIfNoAmmo("ReloadEmpty");
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto Ready;
		// Animate this later
			PISG G 8;
			PISG G 6 A_StartSound("weapons/pistol/mag_out");
			PISG G 10;
			PISG G 4;
			PISG G 3 A_StartSound("weapons/pistol/mag_in");
		Reload.Repeat:
			// if 10 bullets, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_PistolAmmo", invoker.ammo1.MaxAmount, "Reload.Finish");
			// if at least 1 bullet to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto Reload.Finish;
			TNT1 A 0 {
				A_GiveInventory("CFB_PistolAmmo", 1);
				A_Takeinventory("CFB_Magazine",1);
			}
			Goto Reload.Repeat;
		Reload.Finish:
		// Animate this later
			PISG G 3;
			PISG G 5;
			PISG GGG 2;
			Goto Ready;
		ReloadEmpty:
		// Check for bullets -- you'll never be here if you're full
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto Ready;
		// Copy the other one's animation but with the slide back
			PISG H 8;
			PISG H 6 A_StartSound("weapons/pistol/mag_out");
			PISG H 10;
			PISG H 4;
			PISG H 3 A_StartSound("weapons/pistol/mag_in");
		ReloadEmpty.Repeat:
			// if 9 bullets, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_PistolAmmo", invoker.ammo1.MaxAmount - 1, "ReloadEmpty.Finish");
			// if at least 1 bullet to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto ReloadEmpty.Finish;
			TNT1 A 0 {
				A_GiveInventory("CFB_PistolAmmo", 1);
				A_Takeinventory("CFB_Magazine",1);
			}
			Goto ReloadEmpty.Repeat;
		ReloadEmpty.Finish:
		// Copy the other one's animation but with the slide back
			PISG H 3;
			PISG H 5;
		// Close the slide
			PISG G 4 A_StartSound("weapons/pistol/slide_fwd");
			PISG GGG 2;
			Goto Ready;

		Flash:
			TNT1 A 2 A_AttachLightDef("Flash", "CFB_GunFlash");
			TNT1 A 0 A_RemoveLight("Flash");
			Stop;
	}
}