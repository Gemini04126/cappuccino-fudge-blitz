// Pistol
// It do be a pistol tho. Has reloading.
class CFB_PistolAmmo : Ammo
{
	Default
	{
		Inventory.Icon "PISGZ0"; // Replace later
		Inventory.Amount 0;
		Inventory.MaxAmount 11;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 11;
		+INVENTORY.IGNORESKILL
	}
}
class CFB_Pistol : CFB_Weapon replaces Pistol
{
	action state A_ReFirePistol()
	{
		if (JustPressed(BT_ATTACK))
			return ResolveState("Fire");
		else if (JustPressed(BT_ALTATTACK))
			return ResolveState("AltFire");
		else
			return ResolveState(null);
	}
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 2;
		Weapon.SelectionOrder 1900;
		Weapon.AmmoType1 "CFB_PistolAmmo";
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 0;
		Weapon.AmmoType2 "CFB_Magazine";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 10;

		Tag "$TAG_PISTOL";
		Obituary "$KILLPISTOL";
		Inventory.PickupMessage "$GOTPISTOL";

		Weapon.UpSound "weapons/pistol/draw";
		Inventory.PickupSound "weapons/pistol/pickup";
		AttackSound "weapons/pistol/fire";
	}
	States
	{
		Spawn:
			PISG Z -1;
			Stop;

		Select:
			TNT1 A 0 A_QuickRaise();
			TNT1 A 0 A_JumpIfNoAmmo("SelectEmpty");
			PISG X 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto Ready;
		SelectEmpty:
			PISG Y 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG F 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			Goto ReadyEmpty;

		Deselect:
			TNT1 A 0 A_JumpIfNoAmmo("DeselectEmpty");
			PISG X 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG X 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;
		DeselectEmpty:
			PISG F 1;
			PISG Y 1 A_WeaponOffset(-10, 42, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-20, 52, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-40, 72, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-60, 92, WOF_INTERPOLATE);
			PISG Y 1 A_WeaponOffset(-90, 122, WOF_INTERPOLATE);
			TNT1 A 0 A_WeaponOffset(0, 32);
			TNT1 A 0 A_QuickLower();
			Stop;

		Ready:
			TNT1 A 0 A_JumpIfNoAmmo("ReadyEmpty");
			PISG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		ReadyEmpty:
			PISG F 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;

		Fire:
			PISG A 0 A_JumpIfNoAmmo("FireEmpty");
			PISG B 1 Bright {
				A_FireBullets(1.2, 1.2, -1, 15, "CFB_BulletPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "CFB_BulletTracer", -2.5);
				A_GunFlash();
			}
			PISG C 1 Bright;
			// Go to "Empty" reload state if nothing's in the mag
			PISG A 0 A_JumpIfNoAmmo("PostFireEmpty");
			Goto PostFire;
		PostFire:
			PISG EEEDDDDAAAAAA 1 A_ReFirePistol;
			Goto Ready;
		PostFireEmpty:
			PISG EEEDDDD 1;
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, "ReloadEmpty");
			Goto Ready;
		FireEmpty:
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, "ReloadEmpty");
			PISG F 2 A_StartSound("weapons/pistol/empty");
			PISG DDDF 1;
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, "ReloadEmpty");
			Goto Ready;
		Reload:
		// Check for bullets, and if you're already full.
			TNT1 A 0 A_JumpIfInventory("CFB_PistolAmmo", 0, "Ready");
			TNT1 A 0 A_JumpIfNoAmmo("ReloadEmpty");
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto Ready;
			PISG GHIJ 2;
			PISG L 3 A_StartSound("weapons/pistol/mag_out");
			PISG J 13;
			PISG J 4;
			PISG K 3 A_StartSound("weapons/pistol/mag_in");
		Reload.Repeat:
			// if 10 bullets, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_PistolAmmo", invoker.ammo1.MaxAmount, "Reload.Finish");
			// if at least 1 bullet to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto Reload.Finish;
			TNT1 A 0 {
				A_GiveInventory("CFB_PistolAmmo", 1);
				A_Takeinventory("CFB_Magazine",1);
			}
			Goto Reload.Repeat;
		Reload.Finish:
			PISG JIHG 2;
			Goto Ready;
		ReloadEmpty:
		// Check for bullets -- you'll never be here if you're full
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto Ready;
		// Copy the other one's animation but with the slide back
			PISG MNOP 2;
			PISG R 3 A_StartSound("weapons/pistol/mag_out");
			PISG P 13;
			PISG P 4;
			PISG Q 3 A_StartSound("weapons/pistol/mag_in");
		ReloadEmpty.Repeat:
			// if 9 bullets, finish reload
			TNT1 A 0 A_JumpIfInventory("CFB_PistolAmmo", invoker.ammo1.MaxAmount - 1, "ReloadEmpty.Finish");
			// if at least 1 bullet to load, continue
			TNT1 A 0 A_JumpIfInventory("CFB_Magazine", 1, 1);
			Goto ReloadEmpty.Finish;
			TNT1 A 0 {
				A_GiveInventory("CFB_PistolAmmo", 1);
				A_Takeinventory("CFB_Magazine",1);
			}
			Goto ReloadEmpty.Repeat;
		ReloadEmpty.Finish:
			PISG K 4 A_StartSound("weapons/pistol/slide_fwd");
			Goto Reload.Finish;

		Flash:
			TNT1 A 2 A_AttachLightDef("Flash", "CFB_GunFlash");
			TNT1 A 0 A_RemoveLight("Flash");
			Stop;
	}
}