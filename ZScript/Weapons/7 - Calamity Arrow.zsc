// The Calamity Arrow
// "Just make the BFG a gauss cannon then lol" - Matt Eldrydge
// Primary: Explosive railgun projectile.
// Secondary: Calamity Blade.
class CFB_CalamityArrow : CFB_Weapon replaces BFG9000
{
	int charge;
	action void A_FireWaves()
	{
		A_StartSound("DoomRR/heatwave/fire", CHAN_WEAPON);
		A_Overlay(-2, "PrimaryFlash"); // SecondaryFlash.End

		double angle = 5;
		switch (invoker.charge)
		{
		case 2:
			angle = 12.5;
			break;
		case 3:
			angle = 20;
			break;
		case 4:
			angle = 27.5;
			break;
		case 5:
			angle = 35;
			break;
		}

		// In Legacy of Rust vertical autoaim is completely disabled for Heatwave,
		// presumably to prevent situations where a part of the "wave" is autoaimed
		// and a part isn't (supposedly ugly?)
		// Here instead we calculate the autoaim-affected slope with BulletSlope()
		// and then just unconditinally apply it to all projectiles, so if they
		// do get autoaimed, they'll be autoaimed together:
		double projPitch = BulletSlope();
		for (double ang = -angle; ang <= angle; ang += 5.0)
		{
			// Since autoaim is handled manually, we'll need FPF_NOAUTOAIM:
			// CFB_CalamityArrowWave
			A_FireProjectile("CFB_PlasmaBall", ang, useammo: false, flags: FPF_NOAUTOAIM, pitch: DeltaAngle(pitch, projPitch));
		}

		invoker.charge = 0;
		A_ClearRefire();
	}
	Default
	{
		+WEAPON.BFG
		//+WEAPON.NOAUTOFIRE
		Weapon.SlotNumber 7;
		Weapon.SelectionOrder 2800;
		Weapon.AmmoType "CFB_Cell";
		Weapon.AmmoUse 10;
		Weapon.AmmoGive 100;

		Tag "$TAG_CALAMITYARROW";
		// No Obituary defined here because this is a projectile weapon (and has two attacks)
		Inventory.PickupMessage "$GOTCALAMITYARROW";

		//Weapon.UpSound "";
		//Inventory.PickupSound "";
		// No AttackSound defined here because this is a projectile weapon (and has two attacks)
	}
	States
	{
		Spawn:
			CARG Z -1;
			Stop;
		Select:
			TNT1 A 0 A_Overlay(2, "Spinner", true);
			CARG A 1 A_Raise(18);
			Loop;
		Deselect:
			CARG A 1 A_Lower(18);
			Loop;
		Ready:
			TNT1 A 0 A_Overlay(2, "Spinner", true);
			TNT1 A 0 A_JumpIfNoAmmo("Ready.Empty");
			CARG A 1 {
				A_WeaponReady();
				invoker.charge = 0;
			}
			Loop;
		Ready.Empty:
			TNT1 A 0 A_JumpIfInventory("CFB_Cell", 10, "Ready.Charging");
			TNT1 A 0 A_Overlay(2, "StopSpinner");
			CARG B 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY); // no firing. nooooo firing.
			Loop;
		Ready.Charging:
			TNT1 A 0 A_JumpIfNoAmmo("Ready.Empty");
			TNT1 A 0 A_Overlay(2, "Spinner", true);
			CARG CDEFGHIJK 3/* A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOSWITCH)*/;
			Goto Ready;
		Fire:
			CARG A 2 {
				A_FireRailgun();
				A_Overlay(-2, "PrimaryFlash");
			}
			CARG B 10 A_Overlay(2, "StopSpinner");
			Goto Ready.Charging;
		AltFire:
			CARG A 20
			{
				if (invoker.DepleteAmmo(false))
				{
					invoker.charge++;
					A_Overlay(-2, "SecondaryFlash");
					A_StartSound("DoomRR/heatwave/charge", CHAN_WEAPON);
				}
			}
			TNT1 A 0 
			{
				if (invoker.charge < 5 && invoker.CheckAmmo(PrimaryFire, false))
				{
					A_ReFire();
				}
			}
			CARG O 3 A_FireWaves;
			CARG P 5;
			CARG NML 4;
			TNT1 A 0 A_ReFire;
			goto Ready;
		Spinner:
			CARL ABCDE 2;
			Loop;
		StopSpinner:
			Stop;
		PrimaryFlash:
			CARF AB 1;
			Stop;
		SecondaryFlash:
			TNT1 A 0
			{
				A_OverlayRenderStyle(OverlayID(), STYLE_Add);
				return invoker.FindStateByString("SecondaryFlash.Charge"..clamp(invoker.charge, 1,5));
			}
		SecondaryFlash.Charge1:
			HETC A 6 bright;
			HETC BCD 5 bright;
			goto LightDone;
		SecondaryFlash.Charge2:
			HETC E 6 bright;
			HETC FGH 5 bright;
			goto LightDone;
		SecondaryFlash.Charge3:
			HETC I 6 bright;
			HETC JKL 5 bright;
			goto LightDone;
		SecondaryFlash.Charge4:
			HETC M 6 bright;
			HETC NOP 5 bright;
			goto LightDone;
		SecondaryFlash.Charge5:
			HETC Q 6 bright;
			HETC RST 5 bright;
			goto LightDone;
		SecondaryFlash.End:
			HETD A 3 bright
			{
				A_OverlayRenderStyle(OverlayID(), STYLE_Add);
				A_Light1();
			}
			HETD B 5 bright A_Light2;
			goto LightDone;
	}
}